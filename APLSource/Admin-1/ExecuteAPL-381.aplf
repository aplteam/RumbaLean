 ExecuteAPL←{
     ⍝ ⍺ ←→ Command Line Arguments e.g.: '-b maxws=1g default_io=1'
     ⍝ ⍵ ←→ One or more expressions to execute
     ⍝ ← ←→ Exit Code, Session Output
     ⍺←'ShowStatusOnError=0'
     in←∊(⊆⍵),¨⎕UCS 13
     ⎕USING←'System'∘,¨'' '.IO' '.Diagnostics,System.dll'
     p←⎕NEW Process
     i←p.StartInfo
     i.FileName←2 ⎕NQ'.' 'GetCommandLine'
     i.Arguments←⍺
     i.RedirectStandardOutput←1
     i.RedirectStandardInput←1
     i.RedirectStandardError←1
     i.UseShellExecute←0
     _←p.Start ⍬
     sw←⎕NEW StreamWriter p.StandardInput.BaseStream
     op←{6::0 ⋄ ⍺⍺ ⍵}
     _←sw.Write op⊂in
     _←{6::0 ⋄ z←sw.Close}0
     r←⎕NS''
     sr←⎕NEW StreamReader p.StandardError.BaseStream
     r.SessionOutput←sr.ReadToEnd
     _←p.WaitForExit op ⍬
     r.ExitCode←p.ExitCode
     r
 }

 r←{rf}InstallService servicename;cmd;cl;q;f;z;i;maxWait;success;exe;LXwas;WSIDwas;tempWSID
⍝ Installs Rumba as a service.
⍝ If the right argument "servicename" is empty it defaults to the result of fns `DefaultServiceName`.
⍝ The optional left argument defaults to 0. 1 allows one to RIDE into the running service.
⍝ *** This function temporarily changes ⎕lX and ⎕WSID.
 servicename←GetServiceName servicename
 rf←{0<⎕NC ⍵:⍎⍵ ⋄ 0}'rf'                    ⍝ RIDE flag

 LXwas←⎕LX
 ⎕LX←'#.CarlisleGroup.Rumba.TestAsService.LX'
 WSIDwas←⎕WSID
 tempWSID←⎕WSID←(739⌶0),'/',servicename
 0 ⎕SAVE ⎕WSID
 ⎕WSID←WSIDwas
 ⎕LX←LXwas
 cl←#.GetCommandLineArgs                    ⍝ Current command line
 q←{'"',⍵,'"'}                              ⍝ Quote stuff
 exe←↑cl                                    ⍝ The interpreter
 exe←(exe↑⍨'Dyalog.exe'{1⍳⍨⍺⍷⍵}exe),'Dyalogrt.exe'
 cmd←⊂q exe                                 ⍝
 cmd,←⊂q tempWSID                           ⍝ The workspace
 cmd,←⊂'APL_ServiceInstall=',servicename    ⍝ Tell the interpreter to install the service
 cmd,←⊂'maxws=32MB'
 cmd,←⊂'DYALOG_NOPOPUPS=1'                  ⍝ We don't want the dialog box "You have successfully..." nor any other
 cmd,←rf/⊂'RIDE_INIT=CONNECT:127.0.0.1:4502'⍝ User wants to have a RIDE
 z←⎕CMD(1↓∊' ',¨cmd)''                      ⍝
 i←0                                        ⍝ Counter
 maxWait←10                                 ⍝ Wait for a maximum of .. seconds
 success←0
 :Repeat
     ⎕DL 1
     z←⎕CMD'sc query ',servicename
     success←+/('SERVICE_NAME: ',servicename)⍷∊z
 :Until success∨maxWait=i←i+1
 ⎕←'*** Service ',((~success)/'un'),'successfully installed'
 r←~success
⍝Done

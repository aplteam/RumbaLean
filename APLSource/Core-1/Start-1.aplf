 Start←{
     ⍝ ⍺ ←→ Server
     ⍝ ⍵ ←→ folder with the Conga DLLs. If empty those are assumed in the current directory.
     ⍝ Instead ⍵ can be a vector with one or two items:
     ⍝ ⍵[1] ←→ folder with the Conga DLLs. If empty those are assumed in the current directory.
     ⍝ ⍵[2] ←→ Conga rootname (default is "DEFAULT")
     ⍺←NewServer ⍵
     s←⍺
     _←InitConga 2↑(⊆⍵),⊂'DEFAULT'
     s.LogFileTie←OpenLogFile ⍺ ⍝ Moved up; kai
     _←s HandleStart 0  ⍝ Handle failure!
     s.Connections←⎕NS''
     _←DRC.Close s.Name
     p←s ServerParameters 0
     rc sn←2↑DRC.Srv p
     cs←DRC.Error rc
     0≠rc:⎕SIGNAL⊂('EN' 11)('ENX'rc)('EM'(1⊃cs))('Message'(2⊃cs))
     s.Name←sn
     #.onServiceNotification←'OnServiceNotification'⍺
     _←s LogRumba'Server started'
     s.Tid←ServerLoop&s
     s.StartTime←QTSToSeconds QTS
     _←StartUpstreamServers s
     s
 }

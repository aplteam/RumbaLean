 r←PercentDecode r_;rgx;rgxu;z;t;j;i;lens;fill;m
⍝ Rumba's own function causes a LENGTH ERROR when the data contains both a space and a multi-byte char.
⍝ This function was taken from HttpCommands and slightly changed.
 r←r_
 ((r='+')/r)←' '
 rgx←'[0-9a-fA-F]'
 rgxu←'%[uU]',(4×≢rgx)⍴rgx ⍝ 4 characters
 r←(rgxu ⎕R{{⎕UCS 16⊥⍉16|'0123456789ABCDEF0123456789abcdef'⍳⍵}2↓⍵.Match})r
 i←⍸r='%'
 :If 0≠≢i
     i←(i≤¯2+≢r)/i
 :AndIf 0≠≢i
     j←i∘.+1 2
     z←r[j]
     t←'UTF-8'⎕UCS 16⊥⍉16|'0123456789ABCDEF0123456789abcdef'⍳z
     lens←≢¨'UTF-8'∘⎕UCS¨t  ⍝ UTF-8 is variable length encoding
     fill←i[¯1↓+\0,lens]
     r[fill]←t
     m←(≢r)⍴1
     m[(,j),i~fill]←0
     r←m/r
 :EndIf
